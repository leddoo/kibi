-- @todo: use a struct.
fn mk_board(w, h):
    let values = []
    var i = 0
    while i < w*h:
        values[i] := 0
        i += 1
    end
    return values
end

fn board_print(board, w, h):
    var y = 0
    while y < h:
        var x = 0
        while x < w:
            print(if board[y*w + x] > 0: "#" else: "." end)
            x += 1
        end
        println("")

        y += 1
    end

    println("")
end

fn board_get(board, w, h, x, y):
    if   x <  0: x += w
    elif x >= w: x -= w end

    if   y <  0: y += h
    elif y >= h: y -= h end

    return board[y*w + x]
end

fn board_step(board, w, h):
    let new_board = mk_board(w, h)

    var y = 0
    while y < h:
        var x = 0
        while x < w:
            let neighbors =
                  board_get(board, w, h, x - 1, y - 1)
                + board_get(board, w, h, x    , y - 1)
                + board_get(board, w, h, x + 1, y - 1)
                + board_get(board, w, h, x - 1, y    )
                + board_get(board, w, h, x + 1, y    )
                + board_get(board, w, h, x - 1, y + 1)
                + board_get(board, w, h, x    , y + 1)
                + board_get(board, w, h, x + 1, y + 1)

            if neighbors == 3:
                new_board[y*w + x] = 1
            elif neighbors == 2 and board[y*w + x] == 1:
                new_board[y*w + x] = 1
            end

            x += 1
        end

        y += 1
    end

    return new_board
end

fn sleep():
    var i = 0
    while i < 500000:
        i += 1
    end
end


var w = 10
var h = 10
var board = mk_board(w, h)

board[3*w + 1] = 1
board[3*w + 2] = 1
board[3*w + 3] = 1
board[2*w + 3] = 1
board[1*w + 2] = 1

var i = 0
while i < 2:
    board_print(board, w, h)
    board = board_step(board, w, h)
    --sleep()
    i += 1
end

